/*
 * Copyright 2018 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    ext.kotlin_version = '1.3.0'
    ext.dokka_version = '0.9.17'
    ext.ktlint_version = '0.29.0'

    ext.protobuf_version = '3.6.1'
    ext.protoc_version = '3.6.1'
    ext.protobuf_lite_version = '3.0.1'
    ext.protoc_gen_javalite_version = '3.0.0'
    ext.grpc_version = '1.16.1'
    ext.junit_version = '4.12'

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
    }
}

group 'com.google.api'
version '0.1.0-SNAPSHOT'

apply plugin: 'idea'
apply plugin: 'kotlin'
apply plugin: 'maven'
apply plugin: 'org.jetbrains.dokka'

repositories {
    google()
    mavenCentral()
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    compile project(':kgax-core')
    compile project(':kgax-grpc')
    compile project(':kgax-grpc-android')

    compile project(':examples')
}

archivesBaseName = 'kgax'

allprojects {
    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    test {
        testLogging {
            events "PASSED", "FAILED", "SKIPPED"
        }
    }
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/docs"
    jdkVersion = 8

    packageOptions {
        prefix = "com.google.protobuf"
        suppress = true
    }

    sourceDirs = files(subprojects.collect { p -> new File(p.projectDir, "/src/main/kotlin") })
}
